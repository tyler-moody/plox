Precedence Rules (lowest to highest)

Name            Operators       Associates
Equality        == !=           Left                
Comparison      > >= < <=       Left
Additive        - +             Left
Multiplicative  / *             Left
Unary           ! -             Right

--------------------------------------------------------------------------------
Syntactic Grammar

Each rule in the grammar matches expressions at its precedence or higher

program ->              declaration* EOF ;

declaration ->          variableDeclaration
                        | statement ;                        

statement ->            expressionStatement
                        | printStatement ;

expressionStatement ->  expression ";" ;

printStatement ->       "print" expression ";" ;

expression ->           equality ;

equality ->             comparison (("==" | "!=") comparison)* ;

comparison ->           additive ((">" | ">=" | "<" | "<=") additive)* ;

additive ->             multiplicative (("+" | "-") multiplicative)* ;

multiplicative ->       unary (("/" | "*") unary)* ;

unary ->                ("-" | "!") unary 
                        | primary ;

primary ->              "true" | "false" | "nil" 
                        | NUMBER | STRING
                        | "(" expression ")" 
                        | IDENTIFIER ;
